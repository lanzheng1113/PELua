_SUB Insdriver
     NAME &&fname=%~1
     FIND [ $custom=%&fname% | $custom_64=%&fname% ], CALL hwids &&ID1
     FIND# $Z=Z%2, EXEC =!%programfiles%\7-zip\7z.exe x %1 -o%temp%\pe-driver\%&fname% # EXEC =!%programfiles%\7-zip\7z.exe x %1 -o%2 
     FIND# $Z=Z%2, EXEC =!dpinst.exe /S /Path %temp%\pe-driver\%&fname% # EXEC =!dpinst.exe /S /Path %2
     WAIT 500
     FIND [ $custom=%&fname% | $custom_64=%&fname% ], CALL hwids &&ID2
     FIND |%&ID1%<%&ID2%, EXEC -hide -wait dpinst.exe /S /Path %temp%\pe-driver\%&fname%
     FIND# $DRIVERS_USB=%&fname%, EXEC =!dpinst.exe /S /Path %temp%\pe-driver\%&fname%
     FIND# $Z=Z%2, FILE %temp%\pe-driver
_END

_SUB hwids
    EXEC* &&all=!devcon findall *
    READ  -,0,&&line,%&all% 
    MSTR &&Q=<1>%&line%
    ENVI-ret %1=%&Q%
_END

_SUB InsNicCab
     TEAM TEXT 加载有线网卡驱动.....  #LTRB + $20:Microsoft YaHei UI|WAIT 500
     CALL Insdriver "%ProgramFiles%\DRIVERS_NET.7z"
_END

_SUB Inswifi
     find $3=%&bx64%, envi &&wei=_64! envi &&wei=
     IFEX X:\WifiDriver\DRIVERS_NET_WIRELESS%&wei%.7z,! TEAM LINK %desktop%\搜索安装无线驱动,%&myname%,wifi.wcs,netshell.dll,157,,%programfiles% |exit _sub
     TEAM TEXT 加载无线网卡驱动..... #LTRB + $20:Microsoft YaHei UI|WAIT 500
     CALL Insdriver X:\WifiDriver\DRIVERS_NET_WIRELESS%&wei%.7z
	 CALL Insdriver X:\WifiDriver\DRIVERS_NET_WIRELESS%&wei%_2.7z
     call sload
_END

_SUB sload
     TEAM TEXT 启动无线服务..... #LTRB + $20:Microsoft YaHei UI|WAIT 500
     EXEC =!net start Wlansvc
     WAIT 100
     forx !\WifiDriver\*.xml,&&con,1,set &&exi=%&con%
     find $Z=Z%&exi%, exit _sub
     FDRV &&fDrive=%&exi%
     TEAM TEXT 加载无线连接配置.....#LTRB + $20:Microsoft YaHei UI|WAIT 500
     forx %&fdrive%\WifiDriver\*.xml,&&con,0,exec !cmd.exe /c netsh wlan add profile filename=%&con%
_END

_SUB customCab
     TEAM TEXT 加载自定义驱动..... #LTRB + $20:Microsoft YaHei UI|WAIT 500
     FDRV &&fDrive=%1
     IFEX %&fDrive%\WifiDriver\custom_64.7z, CALL InsDriver %&fDrive%\WifiDriver\custom_64.7z! CALL Insdriver %1
_END

_SUB rtuins
    FILE -m %windir%\inf\rtux64w10.inf;%windir%\inf\rtux64w10.sys=>%temp%
    EXEC =*drvload %temp%\rtux64w10.inf
    FILE -m %temp%\rtux64w10.inf;%temp%\rtux64w10.sys
_END

_SUB autorun
     REGI $HKLM\SYSTEM\CurrentControlSet\Control\PEBootServerAddr,&&ServerAddr
     REGI $HKCU\Software\TightVNC\Control\ReverseConnectionHistory\0=%&ServerAddr%
     exec =!tftp -i %&ServerAddr% get pxeautorun.txt %windir%\system32\pxeautorun.cmd
     wait 500
     ifex %windir%\system32\pxeautorun.cmd, exec pxeautorun.cmd %&ServerAddr%
     exit file
_END

_SUB skraid
    exec* &&smbus=!devcon status PCI\VEN_8086*DEV_A123 PCI\VEN_8086*DEV_9D23 PCI\VEN_8086*DEV_A2A3
    SED &&U=?,VEN_8086,,%&smbus%
    ifex $0=%&U%, exit _sub
    CALL Insdriver "%ProgramFiles%\DRIVERS_RAID.7z" %SystemDrive%\raid
    call wini iaStorAC
_END

_SUB araid
    CALL Insdriver "%ProgramFiles%\DRIVERS_RAID.7z" %SystemDrive%\raid
    exec* &&raid=!devcon status *CC_0104
    SED &&T=?,VEN_1022&DEV_7805,,%&raid%
    ifex $0=%&T%,! call wini amd
    SED &&U=?,VEN_8086&DEV_A,,%&raid%
    ifex $0=%&U%,! call wini iaStorAC
    SED &&U=?,VEN_8086&DEV_9,,%&raid%
    ifex $0=%&U%,! call wini iaStorAC
_END

_SUB sapple
    CALL Insdriver "%ProgramFiles%\DRIVERS_RAID.7z" %SystemDrive%\raid
    exec* &&apple=!devcon status PCI\VEN_106B*CC_0180
    SED &&T=?,Name,,%&apple%
    ifex $0=%&T%,! call wini AppleSSD64
_END

_SUB wini
     WRIT X:\Program Files\WinNTSetup\WinNTSetup.ini,1,[WinNT6]
     WRIT X:\Program Files\WinNTSetup\WinNTSetup.ini,$2,Drivers=X:\raid\%1
_END

_SUB EDPI
 regi HKCU\Software\Microsoft\Windows NT\CurrentVersion\TaskManager\Preferences!
 set &&inifile=%ProgramFiles%\TotalCommander\inifile.exe
 ifex [ $%1>0x60 & $%1<0x84 ],
 {
  exec *%&inifile% "%ProgramFiles(x86)%\DesktopInfo\desktopinfo.ini" [options] width=600
  exec *%&inifile% "%ProgramFiles%\TotalCommander\WINCMD.INI" [AllResolutions] Iconsize32=32
  exec *%&inifile% "%ProgramFiles%\TotalCommander\WINCMD.INI" [Lister] Font1=新宋体,-20,134
  exec *%&inifile% "%ProgramFiles%\TotalCommander\WINCMD.INI" [Lister] Font2=新宋体,-20,134
  exec *%&inifile% "%ProgramFiles%\TotalCommander\WINCMD.INI" [Lister] Font3=新宋体,-20,134
  exec *%&inifile% "%ProgramFiles%\TotalCommander\WINCMD.INI" [Buttonbar] Buttonheight=33
  exec *%&inifile% "%ProgramFiles%\TotalCommander\WINCMD.INI" [Buttonbar] SmallIcons=0
  }
 ifex [ $%1>0x84 & $%1<0xa8 ],
 {
  exec *%&inifile% "%ProgramFiles(x86)%\DesktopInfo\desktopinfo.ini" [options] width=750
  exec *%&inifile% "%ProgramFiles%\TotalCommander\WINCMD.INI" [AllResolutions] Iconsize32=32
  exec *%&inifile% "%ProgramFiles%\TotalCommander\WINCMD.INI" [Lister] Font1=新宋体,-24,134
  exec *%&inifile% "%ProgramFiles%\TotalCommander\WINCMD.INI" [Lister] Font2=新宋体,-24,134
  exec *%&inifile% "%ProgramFiles%\TotalCommander\WINCMD.INI" [Lister] Font3=新宋体,-24,134
  exec *%&inifile%  %Windir%\tlb\toolbars\start\setup.ini [settings] menuItemMarginY=12
  }
 ifex $%1>0xa8, 
 { 
  exec *%&inifile% "%ProgramFiles(x86)%\DesktopInfo\desktopinfo.ini" [options] width=920
  exec *%&inifile% "%ProgramFiles%\TotalCommander\WINCMD.INI" [AllResolutions] Iconsize32=48
  exec *%&inifile% "%ProgramFiles%\TotalCommander\WINCMD.INI" [Lister] Font1=新宋体,-32,134
  exec *%&inifile% "%ProgramFiles%\TotalCommander\WINCMD.INI" [Lister] Font2=新宋体,-32,134
  exec *%&inifile% "%ProgramFiles%\TotalCommander\WINCMD.INI" [Lister] Font3=新宋体,-32,134
  }
_END


//开始
FORX !\boot\wall\winpe.jpg,logo,1,logo %logo%
//RAMD ImDisk,L685,NTFS,Y:,PETOOLS

set &sum=0
LOGS --t=1 %windir%\pe.log
//LOGO -trans:40 @OpenWithToastLogo.png
SCRN &&ScrW,&&ScrH 
ifex #%&ScrH%>%&ScrW%, exec %programfiles%\Rotate\rotate.exe C
TEXT 正在初始化.... #LTRB + $20:Microsoft YaHei UI
FORX %windir%\inf\oem*.inf,,0,CALC #sum=%sum% + 1
INIT I
EXEC !startnet.cmd
TEAM ENVI $APPDATA=X:\Users\Default\AppData\Roaming|ENVI $
EXEC WinXShell.exe
EXEC regedit /s "%ProgramFiles%\Classic Shell\cs.reg"

//增加
//隐藏回收站 网络
regi hklm\Software\Microsoft\Windows\CurrentVersion\Explorer\HideDesktopIcons\NewStartPanel\{645FF040-5081-101B-9F08-00AA002F954E}=1
regi hklm\Software\Microsoft\Windows\CurrentVersion\Explorer\HideDesktopIcons\NewStartPanel\{F02C1A0D-BE21-4350-88B0-7367FC96EF3B}=1
//开始菜单收藏夹隐藏
regi hkcu\Software\IvoSoft\ClassicStartMenu\Settings\Favorites=Hide

CALL $%ProgramFiles%\Classic Shell\StartMenuHelper64.dll
EXEC %ProgramFiles%\Classic Shell\ClassicStartMenu.exe
EXEC =!winpeshl.exe
FILE %PUBLIC%\desktop\desktop.ini
exec* &&dusb=!devcon status PCI\VEN_1B73*DEV_1* PCI\VEN_1106*DEV_9202 PCI\VEN_1106*DEV_9201 PCI\VEN_1106*DEV_3432 PCI\VEN_1106*DEV_3483 PCI\VEN_1106*DEV_3515
SED &&R=?,Name,,%&dusb%
ifex $0=%&R%,! CALL Insdriver "%ProgramFiles%\DRIVERS_USB.7z"
EXEC =%programfiles%\ShowDrivs_Gui_x64.exe MOUN-udm -findboot -w \\.\PhysicalDrive* C-
REGI $HKLM\SYSTEM\CurrentControlSet\Control\SystemStartOptions,&&udm
SED &&R=?,UDMBOOT,,%&udm%
ifex $0=%&R%,SHOW U:-1
exec* &&raid=!devcon status *CC_0104 PCI\VEN_1022*DEV_7803 PCI\VEN_1B21*DEV_06*
SED &&U=?,problem,,%&raid%
SED &&S=?,running,,%&raid%
ifex $0=%&U%,! call araid
ifex $0=%&S%,! call skraid
exec* &&apple=!devcon status PCI\VEN_144D* PCI\VEN_106B*CC_0180
SED &&T=?,Name,,%&apple%
ifex $0=%&T%,! call sapple
FILE %windir%\inf\msdri.inf
FORX !\WifiDriver\custom*.7z,&&ccab,1,envi &&cucab=%&ccab%
find $Z=Z%&cucab%,! call customCab %&cucab%
exec* &&yxwk=!devcon status *CC_0200
SED &&V=?,problem,,%&yxwk%
ifex $0=%&V%,! call InsNicCab
exec* &&wxwk=!devcon status *CC_0280
SED &&W=?,problem,,%&wxwk%
exec* &&usbwk=!find "msdri.inf'" %windir%\inf\setupapi.dev.log
SED &&US=?,0x00,,%&usbwk%
 ifex $0=%&US%,!
 {
  WRIT %temp%\line.txt,$+0,%&usbwk%
  exec* &&line=!find "Published" %temp%\line.txt
  MSTR -delims:' &&n1,&&n2,&&n3,&&n4=<1*>%line%
  EXEC !devcon dp_delete %&n4%
  FILE %temp%\line.txt
  }
SED &&Y=?,running,,%&wxwk%
ifex $0=%&Y%,! call sload
ifex [ $0<>%&W% | $0<>%&US% ], call Inswifi
ifex %windir%\system32\drivers\rtux64w10.sys, ! call rtuins
exec *drvload %windir%\inf\hdaudss.inf %windir%\inf\hdaudio.inf
LOOP $1=1,
{ 
  ifex #%sum%=0, exit loop
  CALC #sum = %sum% - 1
  REGI *HKLM\DRIVERS\DriverDatabase\DriverInfFiles\oem%sum%.inf\,&&ii
  find $Z=Z%&ii%,! exec *drvload %windir%\inf\oem%sum%.inf
}

//删除五笔注册表  

regi #HKEY_CURRENT_USER\Control Panel\Desktop\WindowMetrics\AppliedDPI,&&dpi
ifex $%&dpi%<>0x60, call EDPI %&dpi%


//桌面
LINK %desktop%\网卡万能驱动,%ProgramFiles%\Network\[Network]WanDrv6.exe
link %desktop%\浏览器,%ProgramFiles(x86)%\TheWorld\Application\theworld.exe,,%ProgramFiles(x86)%\TheWorld\ico.ico
LINK %desktop%\加载无线网卡,%windir%\system32\pecmd.exe,"%ProgramFiles%\wifi.wcs",%windir%\system32\netshell.dll#157
LINK %Desktop%\宽带连接,RASPHONE.PBK

//程序
LINK %programs%\实用工具\网络管理,%ProgramFiles%\PENetwork\PENetwork.exe
LINK %programs%\硬件驱动\网卡万能驱动,%ProgramFiles%\Network\[Network]WanDrv6.exe
LINK %programs%\硬件驱动\加载无线网卡,%windir%\system32\pecmd.exe,"%ProgramFiles%\wifi.wcs",%windir%\system32\netshell.dll#157
LINK %programs%\实用工具\任务管理器,%windir%\System32\taskmgr.exe
LINK %programs%\实用工具\7z文件,%ProgramFiles%\7-zip\7zFM.exe
link %programs%\实用工具\浏览器,%ProgramFiles(x86)%\TheWorld\Application\theworld.exe,,%ProgramFiles(x86)%\TheWorld\ico.ico
LINK %programs%\实用工具\记事本,%windir%\notepad.exe
LINK %programs%\实用工具\屏幕键盘,%windir%\FreeVK.exe
LINK %programs%\实用工具\截图工具,%&myname%,%windir%\wall.ini sn,shell32.dll,141
HOTK Win + x,LOAD %windir%\wall.ini sn
LINK %programs%\系统维护\注册表编辑工具,%windir%\regedit.exe
LINK %sendto%\发送到桌面快捷方式,%&myname%,%windir%\wall.ini sd
//

regi HKLM\SYSTEM\CurrentControlSet\Control\Class\{4d36e96b-e325-11ce-bfc1-08002be10318}\0000\DriverDate,&&sd
ifex $%&sd%=,LINK %QuickLaunch%\虚拟键盘, %&myname%,%windir%\wall.ini vk,freevk.exe#0
SITE %ProgramData%\Microsoft\Windows\Start Menu\Programs\Administrative Tools,+H
SITE %ProgramData%\Microsoft\Windows\Start Menu\Programs\StartUp,+H
SITE %startup%,+H
regi HKLM\SOFTWARE\Microsoft\Windows\CurrentVersion\Policies\Explorer\NoPropertiesMyComputer!
regi #HKCU\Software\Sysinternals\PsSuspend\EulaAccepted=0x01
EXEC !cmd /c "%programfiles%\hotplug.bat" pci
EXEC -hide taskmgr.exe
TEXT 初始化完成 #LTRB + $20:Microsoft YaHei UI
wait 500
SERV !IKEEXT
exec *drvload %windir%\inf\netrndis.inf %windir%\inf\wceisvista.inf
regi #HKLM\SYSTEM\ControlSet001\Services\IKEEXT\start=0x03
REGI $HKLM\SYSTEM\CurrentControlSet\Control\PEBootType,&&Boottype
kill taskmgr.exe
FIND $Remote=%&Boottype%,call autorun

//
FORX !\boot\wall\winpe.jpg,logo,1,wall %logo%

TEXT
TEAM TEXT
TEAM TEXT 正在加载相关工具.....  #LTRB + $20:Microsoft YaHei UI|WAIT 500

SHOW -1:-1

EXEC -wait -wd:X:\PEPrograms\sd X:\PEPrograms\sd\sd.exe main.lua

//forx *.*.*.*.cmd,&&bat,1 ,team NAME &&fNam=%&bat% | exec %&bat% %&fNam%

EXIT FILE 